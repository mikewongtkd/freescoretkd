#! /usr/bin/perl

use lib qw( /usr/local/freescore/lib );
use Mojolicious::Lite;
use Mojo::IOLoop::Delay;
use Try::Tiny;
use FreeScore;
use FreeScore::Config;
use FreeScore::Client::Registry;
use FreeScore::Forms::FreeStyle;
use FreeScore::Forms::FreeStyle::RequestManager;
use JSON::XS;
use Digest::SHA1 qw( sha1_hex );
use List::MoreUtils (qw( first_index ));
use Data::Dumper;
use Data::Structure::Util qw( unbless );
use Clone qw( clone );
use EV;

our $json     = new JSON::XS();
our $config   = new FreeScore::Config();
our $registry = new FreeScore::Client::Registry();
our $host     = FreeScore::Config::host();
our $DEBUG    = 0;

# ------------------------------------------------------------
# MANAGE RINGS, DIVISIONS, AND ATHLETES VIA WEBSOCKETS
# ------------------------------------------------------------
websocket '/freestyle/:tournament/:ring/:role' => sub {
	my $self       = shift;
	my $tournament = $self->param( 'tournament' );
	my $ring       = $self->param( 'ring'       );
	my $client     = $registry->add( $self, $tournament, $ring );
	my $manager    = new FreeScore::Forms::FreeStyle::RequestManager( $tournament, $ring, $client );
	my $progress   = undef;

	$self->inactivity_timeout( 3600 ); # 1 hour

	# ----------------------------------------
	# Handle messages
	# ----------------------------------------
	$self->on( message => sub {
		my $self    = shift;
		my $request = $json->decode( shift );

		$request->{ tournament } = $tournament;

		# ===== SHOW A HUMAN-READABLE VERSION OF THE REQUEST
		my $log = clone( $request );
		$log->{ cookie }{ id } = substr( $log->{ cookie }{ id }, 0, 5 ) . '...' if exists $log->{ cookie }{ id };
		$log->{ id } = substr( $log->{ id }, 0, 5 ) . '...' if exists $log->{ id };
		print STDERR $json->canonical->encode( $log ), "\n" if $DEBUG;

		# ===== READ TOURNAMENT SETTINGS
		try   {
			$progress = new FreeScore::Forms::FreeStyle( $tournament, $ring );
		} catch { $client->send( { json => { error => "Error reading database '$tournament': $_" }}); };

		# ===== HANDLE REQUEST
		try   { $manager->handle( $request, $progress ); }
		catch { $client->send( { json => { error => "Error while processing request: $_\n", request => $request }}); };
		print STDERR "\n" if $DEBUG;
	});

	# ----------------------------------------
	# Handle disconnects
	# ----------------------------------------
	$self->on( finish => sub { $registry->remove( $client ); });
};

# ============================================================
# STATUS
# ============================================================
get '/status' => sub {
	my $self = shift;
	$self->res->headers->header( 'Access-Control-Allow-Origin' => $config->host() );
	$self->render( text => 'OK' );
};

# ============================================================
# ERROR TRAPPING
# ============================================================
any '*command' => sub {
	my $self = shift;
	my $command = $self->param( 'command' );
	$self->res->headers->header( 'Access-Control-Allow-Origin' => $config->host() );
	$self->render( json => { error => "FreeScore FreeStyle Service Error: Unknown command: $command" });
};

# ============================================================
# HYPNOTOAD SERVER
# ============================================================
mkdir '/var/log/freescore' unless -e '/var/log/freescore';
app->config( hypnotoad => { listen => [ 'http://*:3082' ], pid_file => '/var/run/freestyle.pid', workers => 1 });
app->log( new Mojo::Log( path => '/var/log/freescore/freestyle.log', level => 'debug' ));
app->start();
