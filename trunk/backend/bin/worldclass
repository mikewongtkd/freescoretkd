#! /usr/bin/perl

use lib qw( /usr/local/freescore/lib );
use Mojolicious::Lite;
use Mojo::IOLoop::Delay;
use Try::Tiny;
use FreeScore;
use FreeScore::Config;
use FreeScore::Client::Registry;
use FreeScore::Forms::WorldClass;
use FreeScore::Forms::WorldClass::RequestManager;
use PHP::Session;
use JSON::XS;
use Digest::SHA1 qw( sha1_hex );
use List::MoreUtils (qw( first_index ));
use MIME::Base64 qw( encode_base64 decode_base64 );
use Data::Dumper;
use Clone qw( clone );
use EV;

our $json     = (new JSON::XS())->boolean_values( 0, 1 );
our $client   = {};
our $config   = new FreeScore::Config();
our $registry = new FreeScore::Client::Registry();
our $DEBUG    = 0;

# ------------------------------------------------------------
# MANAGE RINGS, DIVISIONS, AND ATHLETES VIA WEBSOCKETS
# ------------------------------------------------------------
websocket '/worldclass/:tournament/:ring/:role' => sub {
	my $self       = shift;
	my $tournament = $self->param( 'tournament' );
	my $ring       = $self->param( 'ring'       );
	my $client     = $registry->add( $self );
	my $manager    = new FreeScore::Forms::WorldClass::RequestManager( $tournament, $ring, $client );
	my $progress   = undef;
	my $staging    = undef;

	$self->inactivity_timeout( 3600 ); # 1 hour
	$client->ping->start();

	# ----------------------------------------
	# Handle messages
	# ----------------------------------------
	$self->on( message => sub {
		my $self    = shift;
		my $request = $json->decode( shift );

		# ===== REJECT UNAUTHORIZED USERS
		my $security   = $config->security();
		if( $security->enabled() ) {
			unless( $security->authenticated( $client ) && $security->authorized( $ring )) {
				$client->send( { json => { error => $security->unauthorized(), request => $request }});
				return;
			}
		}

		$request->{ tournament } = $tournament;
		$request->{ ring }       = $ring;
		$request->{ from }       = { cid => $client->cid(), role => $client->role()};

		# ===== READ PROGRESS
		try   { 
			$progress = new FreeScore::Forms::WorldClass( $tournament, $ring ); 
			if( $request->{ type } eq 'ring' && $ring ne 'staging' ) {
				$staging = new FreeScore::Forms::WorldClass( $tournament, 'staging' );
				push @{$progress->{ divisions }}, @{$staging->{ divisions }};
			}
		} catch { $client->send( { json => { error => "Error reading database '$tournament', ring $ring: $_" }}); };

		# ===== HANDLE REQUEST
		try   { $manager->handle( $request, $progress, $client->group() ); }
		catch { $client->send( { json => { error => "Error while processing request: $_\n", request => $request }}); };
		print STDERR "\n" if $DEBUG;
	});

	# ----------------------------------------
	# Handle disconnects
	# ----------------------------------------
	$self->on( finish => sub { 
		my $request = { type => 'users', action => 'update', ring => $ring, user => { action => 'disconnect', sessid => $client->sessid(), id => $client->id(), role => $client->role() }};
		$registry->remove( $client );
		$manager->broadcast_updated_users( $request, $progress, $client->group());
		$client->ping->quit();
	});
};

# ============================================================
# STATUS
# ============================================================
get '/status' => sub {
	my $self = shift;
	$self->res->headers->header( 'Access-Control-Allow-Origin' => $config->host() );
	$self->render( text => 'OK' );
};

# ============================================================
# ERROR TRAPPING
# ============================================================
any '*command' => sub {
	my $self = shift;
	my $command = $self->param( 'command' );
	$self->res->headers->header( 'Access-Control-Allow-Origin' => $config->host() );
	$self->render( json => { error => "FreeScore Worldclass Service Error: Unknown command: $command" });
};

# ============================================================
# HYPNOTOAD SERVER
# ============================================================
mkdir '/var/log/freescore' unless -e '/var/log/freescore';
app->config( hypnotoad => { listen => [ 'http://*:3088' ], pid_file => '/var/run/worldclass.pid', workers => 1 });
app->log( new Mojo::Log( path => '/var/log/freescore/worldclass.log', level => 'debug' ));
app->start();
